buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'de.interactive_instruments', name: 'gradle-plugin-osgi-ipojo', version: '1.12.1.26'
        classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.12.1"
    }
}

apply plugin: 'application'

version = '1.0.0'
group = 'de.interactive_instruments'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "http://maven.forgerock.org/repo/repo"
    }
    maven {
        url "https://dl.bintray.com/iide/maven"
    }
}

configurations {
    bundles
    bundlesRuntime
}

dependencies {
    // TODO: are all the jars in lib dependencies of this?
    compile group: 'de.interactive_instruments', name: 'xtraplatform-runtime', version: '1.0.1'

    bundlesRuntime group: 'de.interactive_instruments', name: 'xtraplatform-bundles-runtime', version: '1.0.3'

    bundles(group: 'de.interactive_instruments', name: 'xtraplatform-bundles-core', version: '1.0.1') {
        // TODO
        exclude module: 'xtraplatform-kvstore-inmemory'
    }
    //bundles group: 'de.interactive_instruments', name: 'xtraplatform-bundles-devel', version: '1.0.2'
    bundles group: 'de.interactive_instruments', name: 'xtraplatform-sdi-tools', version: '1.0.1'

    // ???
    subprojects.each {
        bundles(it) {
            transitive = false
        }
    }
}

// TODO env plugin
configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

mainClassName = "de.ii.xtraplatform.runtime.FelixRuntime"

/*task devBundles(type: Sync) {
    from configurations.bundles.resolvedConfiguration.resolvedArtifacts.findAll({
        it.moduleVersion.id in configurations.bundles.incoming.resolutionResult.root.dependencies.collect({it.selected.dependencies}).collectNested({it.selected.moduleVersion}).flatten()
    }).collect({it.file})
    into "$buildDir/bundles/runtime"
}*/

distributions {
    main {
        contents {
            from(configurations.bundles) {
                into "bundles/platform"
            }
            from(configurations.bundlesRuntime) {
                into "bundles/runtime"
            }
            into('') {
                //create an empty 'data/log' directory in distribution root
                def appDirBase = new File(buildDir, 'tmp/app-dummy-dir')
                def logDir = new File(appDirBase, 'data/log')
                logDir.mkdirs()

                from {appDirBase}
            }
        }
    }
}
distTar.version = ''


task cleanFelixCache(type: Delete) {
    delete new File(installDist.destinationDir, 'data/felix-cache')
}

run {
    dependsOn installDist
    dependsOn cleanFelixCache
    workingDir = installDist.destinationDir
    debug = project.hasProperty('debug')? project.debug : false
}

apply from: 'bundles.gradle'

apply from: 'docker.gradle'



