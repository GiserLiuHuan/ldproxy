
subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'osgi-ipojo'
    //apply plugin: 'biz.aQute.bnd.builder'
    apply plugin: 'license'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = 1.8
    
    group = 'de.interactive_instruments'

    license {
        header rootProject.file('gradle/license-header')
        strictCheck true
        excludes([
                "**/Modifiable*.java",
                "**/Immutable*.java",
                "**/node_modules/**/*",
                "**/*.mustache",
                "**/*.png",
                "**/*.ico",
                "**/*.xcf",
                "**/*.js",
                "**/*.css",
                "**/template.html",
                "**/*.json"])
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        ext.name = "interactive instruments GmbH"

        //ext.name = "European Union, interactive instruments GmbH"
        //includes([
        excludes([
            "**/LdProxyAdminServiceResource.java",
            "**/CatalogResource.java",
            "**/LdProxyServiceResource.java",
            "**/AbstractFeatureWriter.java",
            "**/GeoJsonFeatureWriter.java",
            "**/GeoJsonOnTheFlyMapping.java",
            "**/GetCapabilities2Dataset.java",
            "**/MicrodataFeatureWriter.java",
            "**/JsonLdOnTheFlyMapping.java",
            "**/JsonLdOutputWriter.java",
            "**/IndexValueWriter.java",
            "**/LdProxyModule.java",
            "**/LdProxyService.java",
            "**/LdProxyServiceSerializer.java",
            "**/LdProxyServiceStore.java",
            "**/LdProxyServiceStoreDefault.java"
        ])
    }

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://dl.bintray.com/iide/maven"
        }
        mavenLocal()
    }

    configurations {
        embedded
        provided
        compile.extendsFrom embedded
        compile.extendsFrom provided
    }

    dependencies {
        compileOnly         "org.immutables:value:2.6.3:annotations"
        annotationProcessor "org.immutables:value:2.6.3"

        // TODO: get from root project, at least the versions
        provided group: 'de.interactive_instruments', name: 'xtraplatform-bundles-runtime', version: '1.1.5'
        // TODO: shouldn't we only be allowed to use api bundles here?
        provided group: 'de.interactive_instruments', name: 'xtraplatform-bundles-core', version: '1.2.6'
        provided group: 'de.interactive_instruments', name: 'xtraplatform-sdi-tools', version: '2.1.0'

        // TODO: this is in bundles-runtime, but with transitive=false
        // TODO: we need the transitive dependencies in provided
        provided group: 'de.interactive_instruments', name: 'xtraplatform-dropwizard', version: '1.1.6'

        testCompile group: 'org.testng', name: 'testng', version: '6.8.5'
        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile 'com.greghaskins:spectrum:1.2.0'
        testCompile "org.mockito:mockito-core:+"
        testCompile("org.assertj:assertj-core:+")
        testCompile "org.spockframework:spock-core:1.2-groovy-2.4"
    }

    compileJava {
        String relativePath = "build/generated/src/main/java"
        sourceSets.main.java { srcDir relativePath }
        File generatedSourceDir = project.file("build/generated/src/main/java")
        project.mkdir(generatedSourceDir)
        options.annotationProcessorGeneratedSourcesDirectory = generatedSourceDir
        outputs.dir(generatedSourceDir)

        doFirst {
            // force a clean of generatedSourceDir to prevent java.lang.IllegalStateException: endPosTable already set. This shouldn't be need if using Gradle 4.8+ or Java 9+.
            project.delete(generatedSourceDir)
            project.mkdir(generatedSourceDir)
        }
    }

    task testSpectrum (type: Test) {
        useJUnit()
    }

    test {
        useTestNG()
        dependsOn testSpectrum
        options {
            includeGroups 'default'
            /* if you want to run tests for debugging purposes that are not part
            of the continuous integration, put them in the "debug"-group and
            uncomment the following line just in your local working copy
             */
            //includeGroups 'debug'
            //includeGroups 'performance'
            //includeGroups 'integration'
        }
        testLogging.showStandardStreams = true
        reports.html.enabled = true

    }

    jar {
        manifest {
            attributes("Implementation-Title": project.name, "Implementation-Version": project.version)
            
            instruction '-removeheaders', 'Bnd-LastModified'
            instructionReplace "Bundle-SymbolicName",project.name//.replaceAll('-', '.')

            instruction 'Import-Package', 'com.fasterxml.jackson.module.afterburner.ser'
            instruction 'Import-Package', '*'
            instruction 'Private-Package', 'de.ii.empty'
            /*
            attributes('-removeheaders': 'Bnd-LastModified',
                "Bundle-SymbolicName": project.name.replaceAll('-', '.'),
                'Import-Package': 'com.fasterxml.jackson.module.afterburner.ser, *',
                //'Import-Package': '*',
                'Private-Package': 'de.ii.empty;-split-package:=merge-first, de.ii.' + project.name.replaceAll('-', '.') + '.internal.*',
                'Export-Package': '!de.ii.' + project.name.replaceAll('-', '.') + '.internal.*, de.ii.' + project.name.replaceAll('-', '.') + '*'
                )
            */
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allSource
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                artifact sourceJar {
                    classifier "sources"
                }

                pom.withXml{
                    asNode().remove(asNode().get('dependencies'))
                    def dependenciesNode = asNode().appendNode('dependencies')

                    configurations.provided.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                        dependencyNode.appendNode('scope', 'runtime')

                        def exclusionsNode = dependencyNode.appendNode('exclusions')
                        def exclusionNode = exclusionsNode.appendNode('exclusion')
                        exclusionNode.appendNode('groupId', '*')
                        exclusionNode.appendNode('artifactId', '*')
                    }
                }
            }
        }
    }

    afterEvaluate { project ->
        bintray {
            user = project.bintrayUser
            key = project.bintrayApiKey
            publications = ['maven']
            publish = true
            pkg {
                repo = 'maven'
                name = project.name
                userOrg = 'iide'
                licenses = ['MPL-2.0']
                websiteUrl = 'https://github.com/interactive-instruments/ldproxy'
                issueTrackerUrl = 'https://github.com/interactive-instruments/ldproxy/issues'
                vcsUrl = 'https://github.com/interactive-instruments/ldproxy.git'
                githubRepo = 'interactive-instruments/ldproxy'
                githubReleaseNotesFile = 'README.md'
                version {
                    name = project.version
                }
            }
        }
    }


    /*afterEvaluate {
        task hotDeploy(type: Copy) {
            dependsOn jar
            from jar
            into new File(rootProject.tasks.installDist.destinationDir, 'bundles/platform')
        }
    }*/
}
